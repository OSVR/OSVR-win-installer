cmake_minimum_required(VERSION 3.0.0)
project(OSVR-Installer-Win)

set(CMAKE_INCLUDE_CURRENT_DIR ON)
SET(TYPE "DEV" CACHE STRING "Either client or dev build type")
SET(CORE_VER "v0.6-1114-gc3ae55c" CACHE STRING "OSVR Core version")
SET(OSVR_PACKAGE_NAME "OSVR Runtime")
SET(CPACK_WIX_UPGRADE_GUID "69CA0E2A-65D4-4191-BA1D-DBE4CA856D0C")


add_executable(osvr_server IMPORTED)
set_property(TARGET osvr_server
            PROPERTY IMPORTED_LOCATION "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Server/x64/bin/osvr_server.exe")

add_executable(OSVRTrackerView IMPORTED)
set_property(TARGET OSVRTrackerView
            PROPERTY IMPORTED_LOCATION "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Tracker-Viewer/x64/OSVRTrackerView.exe")

add_executable(EnableOSVRDirectMode IMPORTED)
set_property(TARGET EnableOSVRDirectMode
            PROPERTY IMPORTED_LOCATION "${CMAKE_CURRENT_SOURCE_DIR}/RenderManager/x64/EnableOSVRDirectMode.exe")

add_executable(DisableOSVRDirectMode IMPORTED)
set_property(TARGET DisableOSVRDirectMode
            PROPERTY IMPORTED_LOCATION "${CMAKE_CURRENT_SOURCE_DIR}/RenderManager/x64/DisableOSVRDirectMode.exe")

add_executable(RenderManagerD3DPresentExample3D IMPORTED)
set_property(TARGET RenderManagerD3DPresentExample3D
            PROPERTY IMPORTED_LOCATION "${CMAKE_CURRENT_SOURCE_DIR}/RenderManager/x64/RenderManagerD3DPresentExample3D.exe")

add_executable(osvr_central IMPORTED)
set_property(TARGET osvr_central
            PROPERTY IMPORTED_LOCATION "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Central/x64/osvr_central.exe")

install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/Documentation/"
        COMPONENT "Documentation"
        DESTINATION "Runtime/Documentation")

install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Tracker-Viewer/x86/"
        COMPONENT "Server"
        DESTINATION "Runtime/bin/x86")
install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Tracker-Viewer/x64/"
        COMPONENT "Server"
        DESTINATION "Runtime/bin/x64")

install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Central/x86/"
        COMPONENT "Server"
        DESTINATION "Runtime/bin/x86")
install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Central/x64/"
        COMPONENT "Server"
        DESTINATION "Runtime/bin/x64")

install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Config/"
        COMPONENT "Server"
        DESTINATION "Runtime/config")

#copy all binaries from osvr server for both dev and client builds
install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Server/x86/bin/"
        COMPONENT "Server"
        DESTINATION "Runtime/bin/x86")
install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Server/x64/bin/"
        COMPONENT "Server"
        DESTINATION "Runtime/bin/x64")


#copy binaries from render manager for both dev and client builds
install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/RenderManager/x86/bin/"
        COMPONENT "Server"
        DESTINATION "Runtime/bin/x86")
install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/RenderManager/x64/bin/"
        COMPONENT "Server"
        DESTINATION "Runtime/bin/x64")

install(FILES "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE"
        COMPONENT "Server"
        DESTINATION "Runtime")

install(FILES "${CMAKE_CURRENT_SOURCE_DIR}/NOTICE"
        COMPONENT "Server"
        DESTINATION "Runtime/bin")

# additional components for SDK build
if(${TYPE} STREQUAL "DEV")
    SET(OSVR_PACKAGE_NAME "OSVR SDK")
    SET(CPACK_WIX_UPGRADE_GUID "1825FD58-B969-40B6-AE40-04613561EB82")

    # copy all OSVR Core headers to include dir
    install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Server/x64/include/"
        COMPONENT "Headers"
        DESTINATION "SDK/include")

    # copy all OSVR Core 32/64 bit libs
    install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Server/x86/lib/"
        COMPONENT "Libraries"
        DESTINATION "SDK/lib/x86")
    install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Server/x64/lib/"
        COMPONENT "Libraries"
        DESTINATION "SDK/lib/x64")

    # copy all RenderManager headers to include
    install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/RenderManager/x64/include/"
        COMPONENT "Headers"
        DESTINATION "SDK/include")

    # copy all RenderManager 32/64 bit libs
    install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/RenderManager/x86/lib/"
        COMPONENT "Libraries"
        DESTINATION "SDK/lib/x86")
    install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/RenderManager/x64/lib/"
        COMPONENT "Libraries"
        DESTINATION "SDK/lib/x64")

    # copy OSVR Core 32 and 64 bit share to samples
    install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Server/x86/share/"
        COMPONENT "Samples"
        DESTINATION "SDK/samples/x86")
    install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Server/x64/share/"
        COMPONENT "Samples"
        DESTINATION "SDK/samples/x64")

    set(CPACK_COMPONENTS_ALL Documentation Server Headers Libraries Samples)
    set(CPACK_PACKAGE_FILE_NAME "SDK")

# runtime installer details
else()
    set(CPACK_COMPONENTS_ALL Documentation Server)
    set(CPACK_PACKAGE_FILE_NAME "Runtime")
endif()

set(CPACK_PACKAGE_INSTALL_DIRECTORY "OSVR")
set(CPACK_PACKAGE_VENDOR "Sensics Inc.")
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY ${OSVR_PACKAGE_NAME})
string(REGEX REPLACE "v([0-9]).*" "\\1" CPACK_PACKAGE_VERSION_MAJOR ${CORE_VER})
string(REGEX REPLACE "v[0-9].([0-9]).*" "\\1" CPACK_PACKAGE_VERSION_MINOR ${CORE_VER})
string(REGEX REPLACE "v[0-9].[0-9]-([0-9]+).*" "\\1" CPACK_PACKAGE_VERSION_PATCH ${CORE_VER})
set(CPACK_PACKAGE_NAME ${OSVR_PACKAGE_NAME})

#executables should be set in pairs "exe" "exe name" (specify all executables at once)
set(CPACK_PACKAGE_EXECUTABLES   osvr_server "OSVR Server"
                                osvr_central "OSVR Central"
                                EnableOSVRDirectMode "Enable DirectMode"
                                DisableOSVRDirectMode "Disable DirectMode"
                                RenderManagerD3DPresentExample3D "D3D Example"
                                OSVRTrackerView "Tracker Viewer")
#set(CPACK_CREATE_DESKTOP_LINKS osvr_server)

SET(CPACK_GENERATOR WIX)
SET(CPACK_WIX_PRODUCT_ICON "${CMAKE_CURRENT_SOURCE_DIR}/assets/osvr_server.ico")
SET(CPACK_WIX_UI_DIALOG "${CMAKE_CURRENT_SOURCE_DIR}/assets/osvr_server_icon_Dialog.png")
SET(CPACK_WIX_UI_BANNER "${CMAKE_CURRENT_SOURCE_DIR}/assets/osvr_server_icon_Banner.png")
SET(CPACK_WIX_LICENSE_RTF "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE.rtf")
SET(CPACK_WIX_PATCH_FILE "${CMAKE_CURRENT_SOURCE_DIR}/cpack/ServerEnvVarPatch.xml")
SET(CPACK_WIX_PROGRAM_MENU_FOLDER "OSVR")


INCLUDE(CPack)
