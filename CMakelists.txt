cmake_minimum_required(VERSION 3.0.0)
project(OSVR-Installer-Win)

set(CMAKE_INCLUDE_CURRENT_DIR ON)
SET(TYPE "DEV" CACHE STRING "Either client or dev build type")
SET(CORE_VER "v0.6-1114-gc3ae55c" CACHE STRING "OSVR Core version")
SET(OSVR_PACKAGE_NAME "OSVR Runtime")
SET(BIT "32" CACHE STRING "32 or 64 bit build")


add_executable(osvr_server IMPORTED)
set_property(TARGET osvr_server
            PROPERTY IMPORTED_LOCATION "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Server/osvr_server.exe")

add_executable(OSVRTrackerView IMPORTED)
set_property(TARGET OSVRTrackerView
            PROPERTY IMPORTED_LOCATION "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Tracker-Viewer/OSVRTrackerView.exe")

add_executable(EnableOSVRDirectMode IMPORTED)
set_property(TARGET EnableOSVRDirectMode
            PROPERTY IMPORTED_LOCATION "${CMAKE_CURRENT_SOURCE_DIR}/RenderManager/EnableOSVRDirectMode.exe")

add_executable(DisableOSVRDirectMode IMPORTED)
set_property(TARGET DisableOSVRDirectMode
            PROPERTY IMPORTED_LOCATION "${CMAKE_CURRENT_SOURCE_DIR}/RenderManager/DisableOSVRDirectMode.exe")

add_executable(RenderManagerD3DPresentExample3D IMPORTED)
set_property(TARGET RenderManagerD3DPresentExample3D
            PROPERTY IMPORTED_LOCATION "${CMAKE_CURRENT_SOURCE_DIR}/RenderManager/RenderManagerD3DPresentExample3D.exe")

install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/Documentation/"
        COMPONENT "Documentation"
        DESTINATION "Documentation")

install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Tracker-Viewer/"
        COMPONENT "Server"
        DESTINATION "Server")

#copy all binaries from osvr server for both dev and client builds
install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Server/bin/"
        COMPONENT "Server"
        DESTINATION "Server")

#copy binaries from render manager for both dev and client builds
install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/RenderManager/bin/"
        COMPONENT "Server"
        DESTINATION "Server")

install(FILES "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE"
        COMPONENT "Server"
        DESTINATION ".")

# additional components for dev build
if(${TYPE} STREQUAL "DEV")
    SET(OSVR_PACKAGE_NAME "OSVR SDK")

    # copy all OSVR Core headers to include dir
    install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Server/include/"
        COMPONENT "Headers"
        DESTINATION "include")

    # copy all OSVR Core libs
    install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Server/lib/"
        COMPONENT "Libraries"
        DESTINATION "lib")

    # copy all RenderManager headers to include
    install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/RenderManager/include/"
        COMPONENT "Headers"
        DESTINATION "include")

    # copy all RenderManager libs
    install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/RenderManager/lib/"
        COMPONENT "Libraries"
        DESTINATION "lib")

    # copy OSVR Core share to samples
    install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/OSVR-Server/share/"
        COMPONENT "Samples"
        DESTINATION "samples")

    set(CPACK_COMPONENTS_ALL Documentation Server Headers Libraries Samples)
    set(CPACK_PACKAGE_INSTALL_DIRECTORY "OSVR/SDK")
    set(CPACK_PACKAGE_FILE_NAME "OSVR-SDK-${CORE_VER}-win-${BIT}bit")

#client installer details
else()
    set(CPACK_COMPONENTS_ALL Documentation Server)
    set(CPACK_PACKAGE_INSTALL_DIRECTORY "OSVR/Runtime")
    set(CPACK_PACKAGE_FILE_NAME "OSVR-Runtime-${CORE_VER}-win-${BIT}bit")
endif()

set(CPACK_PACKAGE_VENDOR "OSVR")
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY ${OSVR_PACKAGE_NAME})
string(REGEX REPLACE "v([0-9]).*" "\\1" CPACK_PACKAGE_VERSION_MAJOR ${CORE_VER})
string(REGEX REPLACE "v[0-9].([0-9]).*" "\\1" CPACK_PACKAGE_VERSION_MINOR ${CORE_VER})
string(REGEX REPLACE "v[0-9].[0-9]-([0-9]+).*" "\\1" CPACK_PACKAGE_VERSION_PATCH ${CORE_VER})
set(CPACK_PACKAGE_NAME ${OSVR_PACKAGE_NAME})

#executables should be set in pairs "exe" "exe name" (specify all executables at once)
set(CPACK_PACKAGE_EXECUTABLES   osvr_server "OSVR Server"
                                EnableOSVRDirectMode "Enable DirectMode"
                                DisableOSVRDirectMode "Disable DirectMode"
                                RenderManagerD3DPresentExample3D "D3D Example"
                                OSVRTrackerView "Tracker Viewer")
#set(CPACK_CREATE_DESKTOP_LINKS osvr_server)

SET(CPACK_GENERATOR WIX)
SET(CPACK_WIX_UPGRADE_GUID "69CA0E2A-65D4-4191-BA1D-DBE4CA856D0C")
SET(CPACK_WIX_PRODUCT_ICON "${CMAKE_CURRENT_SOURCE_DIR}/assets/osvr_server.ico")
SET(CPACK_WIX_UI_DIALOG "${CMAKE_CURRENT_SOURCE_DIR}/assets/osvr_server_icon_Dialog.png")
SET(CPACK_WIX_UI_BANNER "${CMAKE_CURRENT_SOURCE_DIR}/assets/osvr_server_icon_Banner.png")
SET(CPACK_WIX_LICENSE_RTF "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE.rtf")
SET(CPACK_WIX_PATCH_FILE "${CMAKE_CURRENT_SOURCE_DIR}/cpack/ServerEnvVarPatch.xml")
SET(CPACK_WIX_PROGRAM_MENU_FOLDER "OSVR")


INCLUDE(CPack)
